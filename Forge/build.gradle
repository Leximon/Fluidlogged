import com.modrinth.minotaur.dependencies.ModDependency
import groovy.json.JsonSlurper

buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '6.0.+'
    id 'com.modrinth.minotaur' version '2.+'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

archivesBaseName = mod_id
version = "${mod_version}-forge-${mod_version_suffix}"

minecraft {
    mappings channel: 'official', version: minecraft_version
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config=${mod_id}.forge.mixins.json"
            mods {
                modClientRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config=${mod_id}.forge.mixins.json"
            mods {
                modServerRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("dev.isxander.yacl:yet-another-config-lib-forge:${project.yacl_version}")
    implementation fg.deobf("maven.modrinth:rubidium:${project.rubidium_version}")
    compileOnly project(":Common")


    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    compileOnly "org.projectlombok:lombok:${project.lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
}

tasks.withType(Javadoc).all { enabled = false } // workaround for now to publish it

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

processResources {
    from project(":Common").sourceSets.main.resources
}

apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.forge.mixins.json"
}

jar {
    manifest {
        attributes([
                "Specification-Title": "${mod_name}",
                "Specification-Vendor": "${mod_author}",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": "${mod_name}",
                "Implementation-Version": "${mod_version}",
                "Implementation-Vendor" :"${mod_author}"
        ])
    }
    finalizedBy 'reobfJar'
}

// publishing to mod sites
modrinth {
    def slurper = new JsonSlurper()

    token = System.getenv("MODRINTH_TOKEN")
    projectId = "BnXpPaut"
    versionNumber = mod_version
    versionName = "Fluidlogged ${mod_version} Forge"
    versionType = mod_version_type
    changelog = file("CHANGELOG.md").getText("UTF-8")
    uploadFile = jar
    gameVersions = slurper.parseText(mod_game_versions)
    loaders = ["forge"]
    dependencies = [
            new ModDependency("1eAoo2KR", "required") // Yet Another Config Lib
    ]
}

curseforge {
    def slurper = new JsonSlurper()
    def gameVersions = slurper.parseText(mod_game_versions)

    apiKey = System.getenv("CURSEFORGE_TOKEN") ?: ""
    project {
        id = "620894"
        releaseType = mod_version_type
        changelogType = "markdown"
        changelog = file("CHANGELOG.md")

        gameVersions.each { addGameVersion it }
        addGameVersion "Java 17"
        addGameVersion "Forge"
        relations {
            requiredDependency "yacl" // Yet Another Config Lib
        }

        mainArtifact(jar) {
            displayName = "[${gameVersions[0]}] Fluidlogged ${mod_version} Forge"
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

afterEvaluate {
    tasks.curseforge620894.dependsOn jar
}